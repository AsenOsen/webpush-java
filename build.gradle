plugins {
    id 'application'
    id 'com.github.johnrengelman.shadow' version '4.0.2'

    // Used by release.gradle
    id 'maven'
    id 'signing'
    id 'io.codearte.nexus-staging' version '0.12.0'
}

if (hasProperty('release')) {
    apply from: 'release.gradle'
}

apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'

group 'nl.martijndwars'
version '3.1.1'

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    // For CLI
    compile group: 'com.beust', name: 'jcommander', version: '1.72'

    // For parsing JSON
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.0'

    // For making async HTTP requests
    compile group: 'org.apache.httpcomponents', name: 'httpasyncclient', version: '4.1.3'

    // For cryptographic operations
    compile group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: '1.54'

    // For creating and signing JWT
    compile group: 'org.bitbucket.b_c', name: 'jose4j', version: '0.5.2'

    // For making HTTP requests
    testCompile group: 'org.apache.httpcomponents', name: 'fluent-hc', version: '4.5.2'

    // For testing, obviously
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.3.1'

    // For running JUnit tests
    testRuntime group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.3.1'

    // For turning InputStream to String
    testCompile group: 'commons-io', name: 'commons-io', version: '2.5'

    // For reading the demo vapid keypair from a pem file
    testCompile group: 'org.bouncycastle', name: 'bcpkix-jdk15on', version: '1.55'

    // For verifying Base64Encoder results in unit tests
    testCompile group: 'com.google.guava', name: 'guava', version: '19.0'
}

wrapper {
    gradleVersion = '4.10.2'
}

compileJava {
    sourceCompatibility = 1.7
    targetCompatibility = 1.7
}

compileTestJava {
    sourceCompatibility = 1.8
}

mainClassName = 'nl.martijndwars.webpush.cli.Cli'

shadowJar {
    // BouncyCastle JAR is signed; it cannot be extracted and merged into the fat JAR.
    dependencies {
        exclude(
            dependency(group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: '1.54')
        )
    }
    manifest {
        attributes 'Class-Path': '../../lib/bcprov-jdk15on-154.jar'
    }
}

test {
    useJUnitPlatform()

    // TODO: Make the other test proper unit tests and remove this filter.
    include '**/SeleniumTests.class'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}
